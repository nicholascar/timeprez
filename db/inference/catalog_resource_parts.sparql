# Link each Resource to Catalog
# Link Catalog to all Resources
# Links system services to each Item as Distributions

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sdo: <https://schema.org/>
PREFIX sys: <https://localhost:5000/>

INSERT {
    GRAPH <urn:local:inferred> {
        ?r a dcat:Resource ;
           dcat:distribution  sys: , sys:sparql , ?cr .

        ?cr dcterms:title ?cr_title .

        ?c dcterms:hasPart ?r .
        ?r dcterms:isPartOf ?c .
    }
}
WHERE {
    GRAPH ?g {
        VALUES ?dcat_type { dcat:Resource dcat:Dataset }
        ?r a ?dcat_type .

        ?c a dcat:Catalog .

        OPTIONAL {
            ?r sdo:codeRepository ?cr .
        }

        OPTIONAL {
            ?r sdo:codeRepository/dcterms:title ?cr_title .
        }
    }
}